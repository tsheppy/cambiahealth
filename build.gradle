import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'com.palantir.docker' version '0.20.1'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'

group 'sheppy.tucker'
version '1.2'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile 'io.cucumber:cucumber-java:3.0.2'
    testCompile 'io.cucumber:cucumber-junit:3.0.2'
    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Main-Class": "com.example.CsvSorter"
        )
    }
}

docker {
    name "tsheppy/cambiahealth:$version"
    files shadowJar.outputs
    buildArgs([VERSION: "$version", BUILD_DATE: "${new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssXXX").format(new Date())}"])
}

shadowJar {
    baseName = project.name
    classifier = null
    version = null
}

task copySamples(type: Copy) {
    from 'src/test/resources/samples/'
    into 'build/test/samples/'
}

test.dependsOn 'docker'
test.dependsOn 'copySamples'
test {
    jacoco {
        excludes = ['**/text/']
    }
    jvmArgs(["-DdockerWorkingDir=${copySamples.destinationDir}", "-DdockerImage=${docker.name}"])
}

checkstyle {
    toolVersion = '8.12'
    sourceSets = [project.sourceSets.main]
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
        html.destination file("${buildDir}/reports/jacoco")
    }
}

defaultTasks 'build', 'jacocoTestReport'
